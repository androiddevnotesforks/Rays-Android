import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp'
    id 'kotlin-kapt'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
}

apply from: secret

android {
    namespace 'com.skyd.rays'
    compileSdk 34

    defaultConfig {
        applicationId "com.skyd.rays"
        minSdk 24
        targetSdk 34
        versionCode 39
        versionName "1.6-beta17"
        flavorDimensions = ["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    signingConfigs {
        release {
            keyAlias secret.sign.RELEASE_KEY_ALIAS
            keyPassword secret.sign.RELEASE_KEY_PASSWORD
            storeFile file('../key.jks')
            storePassword secret.sign.RELEASE_STORE_PASSWORD
        }
    }

    productFlavors {
        GitHub
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "app_release_${variant.productFlavors[0].name.toLowerCase()}_${variant.versionName}.apk"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false           // 使用keep.xml，keep住某些资源文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'    // 一台手机debug release共存
            ndk {
                abiFilters 'armeabi', 'x86', 'x86_64', 'arm64-v8a'
            }
        }
        release {
            signingConfig signingConfigs.release    //签名
            minifyEnabled true
            shrinkResources true       // 使用keep.xml，keep住某些资源文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'DebugProbesKt.bin'
            excludes += 'kotlin-tooling-metadata.json'
            excludes += 'okhttp3/internal/publicsuffix/NOTICE'
            excludes += 'XPP3_1.1.3.3_VERSION'
            excludes += 'XPP3_1.1.3.2_VERSION'
        }
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

tasks.withType(KotlinCompile).configureEach {
    //noinspection GrDeprecatedAPIUsage
    kotlinOptions {
        freeCompilerArgs += "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
        freeCompilerArgs += "-opt-in=androidx.compose.animation.ExperimentalAnimationApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-opt-in=coil.annotation.ExperimentalCoilApi"
        freeCompilerArgs += "-opt-in=androidx.compose.material3.windowsizeclass.ExperimentalMaterial3WindowSizeClassApi"
        freeCompilerArgs += "-opt-in=androidx.compose.foundation.layout.ExperimentalLayoutApi"
        freeCompilerArgs += "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi"
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.FlowPreview"
        freeCompilerArgs += "-opt-in=kotlinx.serialization.ExperimentalSerializationApi"
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }
}

kapt {
    correctErrorTypes true
}

dependencies {

    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:$md3_version"
    implementation "androidx.compose.material3:material3-window-size-class:$md3_version"
    implementation "androidx.compose.material:material-icons-extended:1.5.3"
    implementation "androidx.compose.ui:ui-tooling-preview:$md3_version"
    implementation "com.google.android.material:material:1.10.0"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"
    implementation "androidx.activity:activity-compose:1.8.0"
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation "com.google.dagger:hilt-android:2.48.1"
    kapt "com.google.dagger:hilt-android-compiler:2.48.1"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "androidx.navigation:navigation-compose:2.7.3"
    implementation "androidx.security:security-crypto:1.1.0-alpha06"
    implementation "com.google.accompanist:accompanist-drawablepainter:$accompanist_version"
    implementation "io.coil-kt:coil-compose:2.4.0"
    implementation "io.coil-kt:coil-gif:2.4.0"
    implementation "androidx.profileinstaller:profileinstaller:1.3.1"
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation "androidx.room:room-runtime:2.6.0"
    implementation "androidx.room:room-ktx:2.6.0"
    ksp "androidx.room:room-compiler:2.6.0"
    implementation "com.github.thegrizzlylabs:sardine-android:0.8"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0"
    implementation "com.github.Kyant0:Monet:0.1.0-alpha03"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "com.airbnb.android:lottie-compose:6.1.0"

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:1.0.0"

    // Google ML Kit
    implementation "com.google.mlkit:text-recognition:$mlkit_recognition_version"
    implementation "com.google.mlkit:text-recognition-chinese:$mlkit_recognition_version"
    implementation "com.google.mlkit:text-recognition-japanese:$mlkit_recognition_version"
    implementation "com.google.mlkit:text-recognition-korean:$mlkit_recognition_version"
    implementation 'com.google.mlkit:image-labeling-custom:17.0.1'
    implementation 'com.google.mlkit:segmentation-selfie:16.0.0-beta4'

    // TF Lite
    implementation 'org.tensorflow:tensorflow-lite:2.13.0'
    implementation 'org.tensorflow:tensorflow-lite-support:0.4.4'

    debugImplementation "androidx.compose.ui:ui-tooling:$md3_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$md3_version"
}